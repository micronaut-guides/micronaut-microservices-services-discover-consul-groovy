plugins {
  id "io.github.http-builder-ng.http-plugin" version "0.1.1"
  id "io.spring.dependency-management" version "1.0.6.RELEASE"
  id 'groovy'
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencyManagement {
    imports {
        mavenBom "io.micronaut:micronaut-bom:$micronautVersion"
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.2'
    testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'

    testCompile "io.micronaut:micronaut-runtime-groovy"
    testCompile "io.micronaut:micronaut-http-client"
}
apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"

import io.github.httpbuilderng.http.HttpTask

task consulStatus(type:HttpTask){
    config {
        request.uri = 'http://localhost:8500'
    }
    get {
        request.uri.path = '/v1/status/leader'
        response.success {
            logger.quiet('Consul running')
        }
        response.failure {
            throw new GradleException("Consul is not runnnig")
        }
    }
}

test.dependsOn(consulStatus)

consulStatus.onlyIf {
    !System.getenv("TRAVIS")
}
test.onlyIf {
    !System.getenv("TRAVIS")
}